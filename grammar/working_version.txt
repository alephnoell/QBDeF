<start> ::= <value>* <formula_family>

<value> ::= "value:" <var_name> "=" <expression> ";"

<formula_family> ::= <name> <format> <parameters>* <variables> <blocks> <output_block>

<name> ::= "name:" <family_name> ";"
<format> ::= ("circuit" | "CNF") ";"

<parameters> ::= "parameters:" "{" <parameter_declaration>+ "}"
<parameter_declaration> ::= <var_name> ":" <param_type> ("," <expression>)* ";"

<variables> ::= "variables:" "{" <variable_declaration>+ "}"
<variable_declaration> ::= <var_name> ("(" <indices> ")" ("where" <index_range> ("," <index_range>)*)?)? ";"
<indices> ::= <index> ("," <index>)*
<index_range> ::= <indices> "in" "[" <expression> "," <expression> "]"

<blocks> ::= "blocks:" "{" <block_definition>+ (<block_definition> | <operator_declaration> | <quantifier_declaration>)+ "}"

<block_definition> ::=   "define block" <single_block_def> <conditions>?
                       | "define blocks" <grouping>? "{" <single_block_def>+ "}" <conditions>?
                       
<single_block_def> ::= <block_name> ("(" <indices> ")")? ":=" <block_body> ";"
                      
<block_body> ::= <brick> ("," <brick>)*
<brick> ::=    "all blocks in" <block_name>
             | <negation>? <block_name> ("(" <indices> ")")?
             | <negation>? <var_name>   ("(" <indices> ")")?
             
<grouping> ::= "grouped in" <block_name>

<conditions> ::= "where" <condition> ("," <conditions>)* ";"
<condition> ::=  (<index_range> | <assignment> | <other_condition>)
<other_condition> ::= <expression>
             
<quantifier_declaration> ::=   "block" <block_name> "quantified with" <quantifier> ";"
                             | "blocks in" <block_name> "quantified with" <quantifier> ";"
                             | "blocks" <block_name> "quantified with" <quantifier> ";"
             
<operator_declaration> ::=   "block" <block_name> "operated with" <operator> ";"
						   | "blocks in" <block_name> "operated with" <operator> ";"
                           | "blocks" <block_name> ("," <block_name>)+ "operated with" <operator> ";"

<output_block> ::= "output block:" "{" <block_name> "}"

